plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion build_version.compile_sdk
    buildToolsVersion build_version.build_tools
    defaultConfig {
        multiDexEnabled true
        applicationId config.app.package
        minSdkVersion build_version.min_sdk
        targetSdkVersion build_version.target_sdk
        versionCode config.app.versionCode
        versionName config.app.versionName

        manifestPlaceholders = [
                //友盟
                UMENG_APP_KEY: config.app.umeng_app_key
        ]
        //版本名后面添加一句话，意思就是flavor dimension 它的维度就是该版本号，这样维度就是都是统一的了
        //用来解决All flavors must now belong to a named flavor dimension问题
        flavorDimensions "versionCode"
    }

    signingConfigs {
        release {
            storeFile file('../key/Harper_demo2.jks')
            storePassword '654321'
            keyAlias 'Harper_demo2'
            keyPassword '654321'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        //多渠道打包
        pro {
            buildConfigField "String", "TEST_URL", config.server.testUrl
            buildConfigField "String", "RELEASE_URL", config.server.releaseUrl
            buildConfigField "boolean", "VAR_BOOLEAN", 'false'
            buildConfigField "String", "VAR_STRING", '"我是String"'
        }
    }

    //打包命名
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08"))
            if (variant.buildType.name.equals('debug')) {
                outputFileName = "harper_${date}.apk"
            }
            if (variant.buildType.name.equals('release')) {
                outputFileName = "harper_${versionName}.apk"
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility build_version.version_java
        targetCompatibility build_version.version_java
    }

    //启动dataBinding
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation target.appcompat
    implementation target.material
    implementation target.constraintlayout
    implementation target.multidex

    //分享
    implementation target.umsdk_common
    implementation target.umsdk_asms
    implementation target.umsdk_link
    implementation target.umsdk_share_core
    implementation target.umsdk_share_board
    implementation target.umsdk_share_wx
    implementation target.wechat_opensdk

    api project(':LibBase')
    api project(':LibDb')
    api project(':LibCommon')
    api project(':LibChart')
    implementation 'androidx.palette:palette:1.0.0'
}