plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion build_version.compile_sdk
    buildToolsVersion build_version.build_tools
    defaultConfig {
        multiDexEnabled true
        applicationId config.app.package
        minSdkVersion build_version.min_sdk
        targetSdkVersion build_version.target_sdk
        versionCode config.app.versionCode
        versionName config.app.versionName

        //版本名后面添加一句话，意思就是flavor dimension 它的维度就是该版本号，这样维度就是都是统一的了
        //用来解决All flavors must now belong to a named flavor dimension问题
        flavorDimensions "versionCode"
//        ndk {
//            abiFilters "armeabi","armeabi-v7a","x86","mips"
//        }
    }

    signingConfigs {
        release {
            storeFile file('../key/Harper_demo2.jks')
            storePassword '654321'
            keyAlias 'Harper_demo2'
            keyPassword '654321'
        }
    }

    lintOptions {
//        checkReleaseBuilds false
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    productFlavors {
        //多渠道打包
        dev {
            applicationId "com.sun.demo2"
            manifestPlaceholders = [
                    APP_NAME : "Harpr测试",
                    UMENG_APP_KEY: config.app.umeng_app_key //友盟
            ]
            buildConfigField "String", "Base_URL", config.server.urlTest
            buildConfigField "String", "Base_URL_H5", config.server.urlH5Test
            buildConfigField "boolean", "VAR_BOOLEAN", 'false'
            buildConfigField "String", "VAR_STRING", '"我是String"'
        }
        pro {
            applicationId "com.sun.demo2"
            manifestPlaceholders = [
                    APP_NAME : "Harpr",
                    UMENG_APP_KEY: config.app.umeng_app_key //友盟
            ]
            buildConfigField "String", "Base_URL", config.server.urlRelease
            buildConfigField "String", "Base_URL_H5", config.server.urlH5Release
            buildConfigField "boolean", "VAR_BOOLEAN", 'false'
            buildConfigField "String", "VAR_STRING", '"我是String"'
        }
    }

    //打包命名
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08"))
            if (variant.buildType.name == 'debug') {
                outputFileName = "harper_${date}.apk"
            }
            if (variant.buildType.name == 'release') {
                outputFileName = "harper_${versionName}_${date}.apk"
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            //开启混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility build_version.version_java
        targetCompatibility build_version.version_java
    }

    //启动dataBinding
    dataBinding {
        enabled = true
    }
}

dependencies {
    //引入libs中的aar文件(下拉菜单MaterialSpinner)
    implementation (fileTree(dir: 'libs'))

    implementation target.constraintlayout
    implementation target.multidex
    implementation target.palette
    implementation target.xUtil
    implementation target.lifecycle_extensions

    //分享
    implementation target.umsdk_common
    implementation target.umsdk_asms
    implementation target.umsdk_link
    implementation target.umsdk_share_core
    implementation target.umsdk_share_board
    implementation target.umsdk_share_wx
    implementation target.wechat_opensdk
    //腾讯地图
    implementation target.tencentLocationSdk
    implementation target.tencent_map_vector
    implementation target.easypermissions
    //中文转为拼音
    implementation files('libs/pinyin4j-2.5.0.jar')
    //webSocket
    implementation target.webSocket

    implementation project(':LibBase')
    implementation project(':LibChart')
    implementation project(':LibImg')
    implementation project(':LibRichText')
    implementation project(':LibBdFace')
    implementation project(':LibVideo')
    implementation project(':LibEmoji')
    implementation project(':LibLuckDraw')
}