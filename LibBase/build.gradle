plugins {
    id 'com.android.library'
}

apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion ver.compileSdk
    buildToolsVersion ver.buildTools

    defaultConfig {
        minSdkVersion ver.minSdk
        targetSdkVersion ver.targetSdk
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /*读取config.gradle中的配置文件，在build后生成xml的资源文件*/
    buildTypes.all { buildType ->
        resValue("string", "version_name", ver.versionName)
        //友盟appKey
        resValue("string", "um_app_key", config.app.umeng_app_key)
        //百度人脸识别
        resValue("string", "bd_license_id", config.app.bd_license_id)
        resValue("string", "bd_license_file_name", config.app.bd_license_file_name)
        //腾讯云
        resValue("string", "qClound_secret_id", config.app.qClound_secret_id)
        resValue("string", "qClound_secret_key", config.app.qClound_secret_key)
        resValue("string", "qClound_region", config.app.qClound_region)
        resValue("string", "qClound_bucket", config.app.qClound_bucket)
    }

    compileOptions {
        sourceCompatibility ver.version_java
        targetCompatibility ver.version_java
    }

    greendao {
        //指定数据库schema版本号，迁移等操作会用到
        //新增观测数据升级数据库版本 <原schemaVersion 1>
        schemaVersion ver.greenDaoVersion
        //DaoSession、DaoMaster以及所有实体类的dao生成的目录,默认为你的entity所在的包名
        //daoPackage 包名
        daoPackage 'com.sun.base.db.greendao'
        //这就是我们上面说到的自定义生成数据库文件的目录了，可以将生成的文件放到我们的java目录中，而不是build中，这样就不用额外的设置资源目录了
        //工程路径
        targetGenDir 'src/main/java'
    }

    //启动dataBinding
    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    //VLayout 是阿里出品的基础 UI 框架
    api('com.alibaba.android:vlayout:1.0.4@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
    //中文转为拼音
    api files('libs/pinyin4j-2.5.0.jar')

    api ver.status_bar_compat
    api ver.rxjava
    api ver.rxandroid
    api ver.converter_gson
    api ver.logger
    api ver.adapter_rxjava2
    api ver.tbs
    api ver.glide
    api ver.zxing
    api ver.commons_lang
    api ver.appcompat
    api ver.material
    api ver.constraintlayout
    api ver.smart_refresh
    api ver.annotation
    api ver.eventbus
    api ver.rxpermissions
    api ver.preference
    //数据库
    api ver.greendao
    api ver.generator
    //腾讯云cos存储
    implementation('com.tencent.qcloud:cosxml:5.4.26') {
        exclude group: 'com.tencent.qcloud', module: 'mtaUtils' //关闭 mta 上报功能
    }
}